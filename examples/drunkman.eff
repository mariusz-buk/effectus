; Effectus example
; ----------------
; Player/missile graphics demo

MODULE

DEFINE P0HPOS="70"
DEFINE P1HPOS="170"
DEFINE SPEED="320"

BYTE
  CH=$2FC,
  RAMTOP=$6A,
  GPRIOR=$26F,
  PMBASE=$D407,
  SDMCTL=$22F,
  GRACTL=$D01D,
  HITCLR=$D01E,

  ; COLLISION DETECTION FLAG
  ; P0PL ($D00C) - PLAYER 0 TO PLAYER COLLISION STATUS
  ; P1PL ($D00D) - PLAYER 1 TO PLAYER COLLISION STATUS
  COLLISION,  

  ; PLAYER POSITION STORAGE
  P0, P1, P3, 
  
  ; MISSILE DISTANCE RANGE POSITION
  M3R

CARD PGMMEM

BYTE ARRAY
  PCOLR(3)=$2C0,
  HPOSP(3)=$D000,
  HPOSM(3)=$D004,
  SIZEP(3)=$D008,

; PLAYER DATA
  P0_DATA=[56 68 124 124 124 124 124 56],
  P1_DATA=[28 34 62 62 28 8 8 8 8 28 62],
  P2_DATA=[24 24 24 60 126 126 126 126 66 90 66 126 66 126 126],
  P3_DATA=[36 149 85 62 66 129 129 165 129 153 129 165 153 66 60],

; IN P/M DOUBLE RESOLUTION, MISSILES
; START AT OFFSET OF 384 OF PMBASE 
; 1 BYTE FOR ALL MISSILES
; (2 BITS PER MISSILE)
;
; MISSILE0 (BITS 0 1)
; MISSILE1 (BITS 2 3)
; MISSILE2 (BITS 4 5)
; MISSILE3 (BITS 6 7)

; MISSILE3 DATA
  M3_DATA=[128 128 255 64 64 255 128 128 255 64 64]

; SIMPLE DELAY ROUTINE
PROC DELAY(INT LIMIT)

INT I

FOR I=1 TO LIMIT DO OD

RETURN

; BULLET MOVE ROUTINE
BYTE FUNC BULLET_PROC(INT MOVE BYTE NUM, POS)

SETBLOCK(PGMMEM+384,128,0)
POS==+MOVE
HPOSP(NUM)=POS
DELAY(SPEED)

RETURN(POS)

; MAIN PROGRAM
PROC MAIN()

; TURN OFF P/M GRAPHICS
GRACTL=0

; GRAPHICS MODE 0
GRAPHICS(0)

; HIDE CURSOR
POKE(752,1)

; PUT SOME TEXT ON SCREEN
PRINTF("DRUNK MAN SHooTING NiGhTmAre%E%E")
PRINTE("Hik! Shoot glasses by pressing Hik!")
Print("joystick fire button! Hik!")
POSITION(1,23)
PRINT("PRESS ESCAPE KEY TO WAKE UP")

; SET SAFE PLACE FOR OUR PLAYER
PGMMEM=RAMTOP-12

; BASE ADDRESS (MSB)
PMBASE=PGMMEM

; STARTING ADDRESS OF P/M GRAPHICS
PGMMEM==*256
 
; P/M DOUBLE RESOLUTION
SDMCTL=46

; CLEAR SPACE FOR P/M GRAPHICS
SETBLOCK(PGMMEM+384,768,0)  

; COPY PLAYER DATA TO P/M MEMORY
MOVEBLOCK(PGMMEM+512+46,P0_DATA,8)
MOVEBLOCK(PGMMEM+640+40,P1_DATA,11)
MOVEBLOCK(PGMMEM+768+40,P2_DATA,15)
MOVEBLOCK(PGMMEM+896+85,P3_DATA,15)

; TURN ON P/M GRAPHICS
GRACTL=3

; PLAYERS 0 TO 2 - NORMAL SIZE
;SIZEP(0)=0 SIZEP(1)=0 SIZEP(2)=0

; PLAYER 3 - DOUBLE SIZE
SIZEP(3)=1

; PLAYER COLORS
PCOLR(0)=152 PCOLR(1)=8
PCOLR(2)=228 PCOLR(3)=248

; PLAYER HORIZONTAL INITIAL POSITIONS
P3=90 P0=P0HPOS P1=P1HPOS
HPOSP(0)=P0 HPOSP(1)=P1
HPOSP(2)=120 HPOSP(3)=P3

; NO KEY PRESSED YET
CH=255

DO
  ; MOVE DRUNK MAN LEFT
  IF STICK(0)=11 THEN 
    P3==-1

    IF P3<50 THEN P3=50 FI

    HPOSP(3)=P3
    DELAY(SPEED)

  ; MOVE DRUNK MAN RIGHT
  ELSEIF STICK(0)=7 THEN
    P3==+1

    IF P3>190 THEN P3=190 FI

    HPOSP(3)=P3
    DELAY(SPEED)
  FI

  ; JOYSTICK TRIGGER WAS PRESSED
  ; THE GAME BEGINS... :)
  IF STRIG(0)=0 THEN
    ; CLEAR ALL P/M GRAPHICS COLLISION REGISTERS
    HITCLR=1
    HPOSM(3)=P3

    ; SHOOT BOTTLE AND GLASSES
    ; MISSILE3 IS A BULLET
    M3R=70

    ; BULLET DISTANCE RANGE CHECK
    WHILE M3R>30 
    DO
      DELAY(SPEED)
      
      ; DRAW BULLET
      SETBLOCK(PGMMEM+384,128,0)  
      MOVEBLOCK(PGMMEM+384+M3R,M3_DATA,10)
       
      ; PLAYER0 IS HIT (LEFT GLASS)
      IF (SIZEP(3)=1) AND (P0=P0HPOS) THEN
        SETBLOCK(PGMMEM+384,128,0)

        DO
          P0=BULLET_PROC(1,0,P0)
          COLLISION=PEEK($D00C)
        UNTIL COLLISION>=4  ; STOP NEAR BOTTLE
        OD

      ; PLAYER1 IS HIT (RIGHT GLASS)
      ELSEIF (SIZEP(3)=2) AND (P1=P1HPOS) THEN
        SETBLOCK(PGMMEM+384,128,0)

        DO
          P1=BULLET_PROC(-1,1,P1)
          COLLISION=PEEK($D00D)
        UNTIL COLLISION>=4  ; STOP NEAR BOTTLE
        OD

      ; PLAYER2 IS HIT (BOTTLE)
      ELSEIF SIZEP(3)=4 THEN
        SETBLOCK(PGMMEM+384,128,0)

        IF P0#P0HPOS THEN
          DO    
            P0=BULLET_PROC(-1,0,P0)
          UNTIL P0=P0HPOS
          OD
        FI

        IF P1#P1HPOS THEN
          DO    
            P1=BULLET_PROC(1,1,P1)
          UNTIL P1=P1HPOS
          OD
        FI
      FI
 
      M3R==-1
    ODWHILE

    SETBLOCK(PGMMEM+384,128,0)
  FI
UNTIL CH=28  ; ESCAPE KEY PRESSED 
OD

; TURN OFF P/M GRAPHICS
GRACTL=0

RETURN
